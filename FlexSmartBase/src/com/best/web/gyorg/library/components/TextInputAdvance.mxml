<?xml version="1.0" encoding="utf-8"?>
<s:TextInput xmlns:fx="http://ns.adobe.com/mxml/2009" 
			 xmlns:s="library://ns.adobe.com/flex/spark" 
			 xmlns:mx="library://ns.adobe.com/flex/mx" 
			 skinClass="com.best.web.gyorg.library.skin.ClearAbleTextInputSkin"
			 maxChars="500" height="26" text=""
			 >
	<fx:Metadata>
		[Event(name="clear", type="flash.events.Event")]
	</fx:Metadata>
	<fx:Script>
		<![CDATA[
			import mx.utils.StringUtil;
			
			import spark.events.TextOperationEvent;
			protected override function updateDisplayList(unscaledWidth:Number,
														  unscaledHeight:Number):void
			{
				super.updateDisplayList(unscaledWidth,unscaledHeight);
				if(clearArea != null)clearArea.solidColor = getStyle("color");//跟文本颜色一致
			}
			
			override protected function stateChanged(oldState:String, newState:String, recursive:Boolean):void
			{
				super.stateChanged(oldState,newState,recursive);
				if(clearArea != null){
					if(newState == "normal"){
						clearArea.visible = true;//控制x的状态
					}else{
						clearArea.visible = false;
					}
				}
			}
			
			[SkinPart(required="false")]
			public var clearArea:ClearArea;//样式中对应的实例
			override protected function partAdded(partName:String, instance:Object):void
			{
				super.partAdded(partName, instance);
				if(partName == "clearArea"){
					clearArea.addEventListener(MouseEvent.CLICK,onClickClear,false,0,true);
					//					clearArea.setStyle("includeIn","normal");
					//					clearArea.setCurrentState("normal");
				}
			}
			
			public function clearAll():void{
				this.text = "";//文本被清空
				dispatchEvent(new Event("clear"));
			}
			
			protected function onClickClear(event:Event):void
			{
				clearAll();
			}
			
			override protected function focusInHandler(event:FocusEvent):void{
				super.focusInHandler(event);
				this.selectAll();
			}
			/** 切除掉前后输入的空格  */
			public var cutWhiteSpace:Boolean = true;
			
			override protected function focusOutHandler(event:FocusEvent):void{
				super.focusOutHandler(event);
				checkWhitespace();
			}
			
			private function checkWhitespace():void{
				if(!cutWhiteSpace)return;
				if(text && (StringUtil.isWhitespace(text.charAt(0)) || StringUtil.isWhitespace(text.charAt(text.length - 1))))
				{
					text = StringUtil.trim(text);
				}
			}
			
			
		]]>
	</fx:Script>
</s:TextInput>